; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!


[Setup]
SignTool=SignP3ICLI
AppName=p3icli
AppVerName=p3icli 3.2
AppPublisherURL=http://p3icli.sourceforge.net/
AppSupportURL=http://p3icli.sourceforge.net/
AppUpdatesURL=http://p3icli.sourceforge.net/
AppPublisher=Clark Morgan
DefaultDirName={pf}\p3icli
DefaultGroupName=p3icli
Compression=lzma
SolidCompression=yes
UninstallDisplayIcon={code:exedir}\p3icli.exe

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "C:\cm\git\p3icli\doc\sampleTemplate.potx"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\p3icli.exe"; DestDir: "{code:exedir}"; BeforeInstall: ExeBeforeInstall; Flags: ignoreversion signonce
Source: "C:\cm\git\p3icli\doc\portfolio.potx"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\readme.txt"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "C:\cm\git\p3icli\doc\p3icli_help.html"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\sample_template.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\sample_named_slide.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\sample_web_text.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\sample_web_text_office_2010.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\sample_web_text_office_2013.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\sample_web_text_office_2016_2019.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\resized_pic1_demo_slide.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\original_resize_demo_slide.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\title_and_caption_moved_demo_slide.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\task_scheduler_config.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\doc\ppt_is_sorry.png"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\copying.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\cm\git\p3icli\buglist.txt"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\PPT Picture Insert CLI"; Filename: "{code:exedir}\p3icli.exe"
Name: "{group}\Documentation"; Filename: "{app}\p3icli_help.html"
Name: "{group}\README"; Filename: "{app}\readme.txt"
Name: "{group}\LICENSE"; Filename: "{app}\copying.txt"
Name: "{group}\Bug List"; Filename: "{app}\buglist.txt"
Name: "{userdesktop}\p3icli"; Filename: "{code:exedir}\p3icli.exe"; Tasks: desktopicon

[Run]
Filename: "{code:exedir}\p3icli.exe"; Description: "{cm:LaunchProgram,p3icli}"; Flags: nowait postinstall skipifsilent unchecked

[Code]
function exedir(Default: String): String;
begin
    if IsAdminLoggedOn then
        Result := ExpandConstant('{win}')
    else
        Result := ExpandConstant('{app}');
end;

procedure ExeBeforeInstall();
var
    fname: string; path: string; proj: string;

begin
    if not IsAdminLoggedOn then begin
        fname   := ExtractFileName(CurrentFileName);
        path    := ExpandConstant(ExtractFilePath(CurrentFileName));
        proj    := fname;
        StringChange(proj, '.exe', '');
        MsgBox('Non-Administrator account installation of ' + proj +
               ' noted.' + #13#13 + 'Consequently, ' + fname +
' cannot be copied to the Windows root directory and will instead be copied to ' +
               path + ' .' + #13#13 +
               'To preclude specifying the complete "' +
               ExpandConstant(CurrentFileName) + '" path when starting ' + #13 +
               'this application, consider copying ' + fname +
               ' to a writable folder in your PATH.',
               mbInformation,
               MB_OK);
    end;
end;





